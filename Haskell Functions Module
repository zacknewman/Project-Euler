{- |
Module      : ProjectEuler
Description : Project Euler functions
Copyright   : (c) Zack Newman
License     :

Maintainer  : zacknewman22@gmail.com
Stability   : stable
Portability : portable

The ProjectEuler module contains functions to calculate some of the problems
from Project Euler.
-}

module ProjectEuler where
import Data.List (group, sort)

-- |The sumConsecNumbers function returns the sum of the first n numbers
-- beginning from i. The only parameter that requires a positive integer is n.
-- The function is defined over the Fractional class for the other two
-- parameters. The function returns the sum of i, i + j, i + 2*j,...,
-- i + (n - 1)*j.
sumConsecNumbers      :: (Fractional a) => a -> Integer -> a -> a
sumConsecNumbers i n j = (m + 1) * i + (m ^ (2::Integer) + m) * j / 2
    where m = fromInteger (n - 1)

-- |The fibAll function creates the infinite list of Fibonacci integers.
fibAll :: [Integer]
fibAll  = 0:1:zipWith (+) fibAll (tail fibAll)

-- |The factorization function creates the unsorted list of the prime
-- factorization of any positive integer. This function is a rather simple
-- implementation that can be greatly improved
-- (e.g., general number field sieve), and so should only be
-- used for integers up to 20-digits in length.
factorization  :: Integer -> [Integer]
factorization 1 = []
factorization n = p:factorization (n `div` p)
    where (m, p) = (round $ sqrt (fromInteger n::Double), head $ filter (\ x ->
                    n `mod` x == 0) ((2:3:[5,11..m]) ++ [7,13..m] ++ [n]))

-- |The toDigits function maps a non-negative integer to the list of the
-- integer's digit(s).
toDigits       :: Integer -> [Integer]
toDigits n
    | n < 0     = error "Argument must be a non-negative integer."
    | n > 9     = toDigits i ++ [j]
    | otherwise = [n]
        where (i, j) = n `divMod` 10

-- |The palindromicNumber function tests if a non-negative integer is a
-- palindromic integer.
palindromicNumber  :: Integer -> Bool
palindromicNumber n = toDigits n == reverse (toDigits n)

-- |The palindromeProduct produces the largest palindromic integer that is a
-- product of two non-negative n-digit integers.
palindromeProduct :: Integer -> Integer
palindromeProduct n = maximum $ filter palindromicNumber xs
    where xs = [i * j | i <- [10 ^ (n - 1)..10 ^ n - 1], j <- [10 ^ (n - 1)..10
                ^ n - 1]]

-- |The prime function tests if an integer is prime. This is a very
-- simple function; consequently this function may take a lot of time
-- to compute if the number exceeds 20 digits. In the case of very large
-- integers one must employ sieve theory techniques.
prime  :: Integer -> Bool
prime n = [n] == factorization n

-- |The listPrimes function produces an unsorted list of all primes
-- bounded by an integer.
listPrimes     :: Integer -> [Integer]
listPrimes n
    | n < 2     = []
    | n == 2    = [2]
    | otherwise = filter prime ((2:3:[5,11..n]) ++ [7,13..n])

-- |The allPrimes function produces the list of all primes.
allPrimes :: [Integer]
allPrimes  = filter prime (2:[3,5..])

-- |The smallestMultiple function produces the smallest integer that is a
-- multiple of every integer from 1 to n.
smallestMultiple  :: Integer -> Integer
smallestMultiple n = foldl f 1 $ listPrimes n
    where f = \ q p -> p ^ (floor $ log (fromInteger n::Double) / log
              (fromInteger p::Double)::Integer) * q

-- |The sumConsecSquares function returns the sum of the first n number
-- of squares beginning from i. The only parameter that requires a positive
-- integer is n. The function is defined over the Fractional class
-- for the other two parameters. The function returns the sum of squares of
-- i, i + j, i + 2*j,..., i + (n - 1)*j.
sumConsecSquares      :: (Fractional a) => a -> Integer -> a -> a
sumConsecSquares i n j = (m + 1) * i ^ x + (m ^ x + m) * i * j + (2 * m ^ y + 3
                         * m ^ x + m) * j ^ x / 6
    where (x, y, m) = (2::Integer, 3::Integer, fromInteger (n - 1))

-- |The digitProduct function returns the largest product of m
-- consecutive digits in n.
digitProduct    :: Int -> Integer -> Integer
digitProduct m n = maximum $ f m $ toDigits n
    where f = \ i xs -> if length xs < m then [] else product (take i xs):f i
              (drop 1 xs)

-- |The pythagoreanTriple function returns the tuple containing the
-- primitive Pythagorean triple based on two inputted integers. It is the
-- implementation of the Pythagorean Triples Theorem. The parameters
-- s and t need to be odd integers such that s > t >= 1.
pythagoreanTriple  :: (Integral a) => a -> a -> [a]
pythagoreanTriple s t = [s * t, (s ^ i - t ^ i) `div` 2, (s ^ i + t ^ i) `div`
                        2]
    where i = 2::Integer

-- |The pythagoreanTripleBoundSum function returns the tuple containing
-- the Pythagorean triple whose sum equals the inputted integer or
-- Nothing if there doesn't exist such a triple.
pythagoreanTripleBoundSum  :: (Integral a) => a -> Maybe [a]
pythagoreanTripleBoundSum n
    | v == []   = Nothing
    | otherwise = Just $ pythagoreanTriple s t
        where (m, l, u, j, k, v, s, t) = (fromIntegral n::Double, ceiling ((sqrt
                                         (2 * m + 1) + 1) / 2), floor ((sqrt (4
                                         * m + 1) - 1) / 2), if l `mod` 2 == 0
                                         then l + 1 else l, if u `mod` 2 == 0
                                         then u + 1 else u, filter (\ i ->
                                         ceiling m `mod` i == 0) [j, j + 2..k],
                                         head v, ceiling m `div` s - s)

-- |The transpose function transposes the lists inside of a list.
transpose             :: [[a]] -> [[a]]
transpose []           = []
transpose ([]:xss )    = transpose xss
transpose ((x:xs):xss) = (x:[y | (y:_) <- xss]):transpose (xs:[zs | (_:zs) <-
                         xss])

-- |The rowProductMax function returns the largest product of k adjacent numbers
-- in the rows of any shaped grid.
rowProductMax      :: (Num a, Ord a) => Int -> [[a]] -> a
rowProductMax k xss = maximum $ map (\ xs -> if length xs < k then 0 else
                      maximum $ f k xs) xss
    where f = \ i xs -> if length xs < k then [] else product (take i xs):f i
              (drop 1 xs)
    
-- |The columnProductMax function returns the largest product of k adjacent
-- numbers in the columns of any shaped grid.
columnProductMax      :: (Num a, Ord a) => Int -> [[a]] -> a
columnProductMax k xss = rowProductMax k $ transpose xss

-- |The diagonalProductMax function returns the largest product of k adjacent
-- numbers in the diagonals of an n x n grid.
diagonalProductMax      :: (Num a, Ord a) => Int -> [[a]] -> a
diagonalProductMax k xss = maximum [d1, d2, d3, d4]
    where (f, f2, f3, f4, f5, d1, d2, d3, d4) =
                                                (\ (xs, i) -> drop i xs, \ xs ->
                                                reverse xs, columnProductMax, \
                                                yss -> zip yss [0..length (xss
                                                !! 0) - 1], \ yss -> transpose $
                                                drop 1 yss, f3 k $ map f $ f4
                                                xss, f3 k $ map f $ f4 $ map f2
                                                xss, f3 k $ map f $ f4 $ f5 xss,
                                                f3 k $ map f $ f4 $ f5 $ map f2
                                                xss)

-- |The gridProductMax function returns the largest product of k adjacent
-- numbers an n x n grid.
gridProductMax      :: (Num a, Ord a) => Int -> [[a]] -> a
gridProductMax k xss = maximum [rows, columns, diagonals]
    where (rows, columns, diagonals) = (rowProductMax k xss, columnProductMax
                                        k xss, diagonalProductMax k xss)

-- |The potentialHighlyComposite function returns a list of potential
-- highly composite numbers according to the required forms most famously proven
-- by Srinivasa Ramanujan. The function's primary purpose is its use by
-- by another function "highlyCompositeNumber" to calculate the minimum
-- highly composite number with at least a given number of factors. This
-- functions requires an input for the number of unique primes, an input
-- for the minimum number of factors, and a list of the possible primes
-- the potential highly composite number is composed of.
potentialHighlyComposite       :: Int -> Integer -> [Integer] -> [Integer]
potentialHighlyComposite 1 _ _  = []
potentialHighlyComposite i n xs = [product $ zipWith (^) zs rs | rs <- filter (\
                                   js -> reverse js == sort js) $ sequence
                                   ([[floor ((log (fromInteger $ last
                                   zs)::Double) / (log (fromInteger
                                   j)::Double))..2 * ceiling ((log (fromInteger
                                   $ last ys::Double)) / (log (fromInteger
                                   j)::Double)) - 1] | j <- qs] ++ [[1]]),
                                   product (zipWith (+) [1,1..] rs) >= n] ++
                                   potentialHighlyComposite (i - 1) n xs
    where (ys, zs, qs) = (take (i + 1) xs, init ys, init zs)

-- |The highlyCompositeNumber function returns the minimum highly composite
-- number with at least an integer-number of factors.
highlyCompositeNumber :: Integer -> Integer
highlyCompositeNumber n
    | n == 1    = 1
    | n == 2    = 2
    | n == 3    = 4
    | n == 9    = 36
    | otherwise = minimum (potentialHighlyComposite i n xs)
        where (i, xs) = (floor ((log (fromInteger n)::Double) / log 2), take (i
                         + 1) allPrimes)

-- |The triangularNumberFactors function returns the first triangular number
-- with at least an integer-number of factors. It first invokes the
-- "highlyCompositeNumber" function in order to bypass testing too-small
-- numbers.
triangularNumberFactors  :: Integer -> Integer
triangularNumberFactors n = h $ head $ filter (\ (_, y) -> y > n) $ zip [i..] $
                            map g $ map f $ zipWith (++) xs $ tail xs
    where (i, xs, f, g, h) = (ceiling (((sqrt (fromInteger (1 + 8 *
                              highlyCompositeNumber n))::Double) - 1) / 2) + 1,
                              map factorization [i..], \ ys -> group $ tail $
                              sort ys, \ xss -> if xss == [] then 1 else
                              toInteger (length (head xss) + 1) * g (tail xss),
                              \ (a, _) -> a * (a + 1) `div` 2)

-- |The collatzMaxLength function return the integer with the maximum Collatz
-- sequence length. While the Collatz conjecture hasn't been proven to be true,
-- no counterexample has been found either. Therefore one can "safely"
-- assume this function will always produce a value. Since a list of Collatz
-- lengths will be created for every integer < n, time and memory will be an
-- issue for larger inputs. This function repeatedly composes the Collatz
-- function on itself until a value of 1 is reached for every number less than
-- n. A list containing the most compositions and the responsible value is
-- returned. To speed up this function, an array should be used; however
-- if one compiles the main module using the "O2" flag, then the time isn't
-- that much greater.
collatzMaxLength  :: Integer -> [Integer]
collatzMaxLength n = maximum [collatz i | i <- [1..n - 1]]
    where collatz 1 = [1, 1]
          collatz m
            | even m    = [1 + head (collatz (m `div` 2)), m]
            | otherwise = [2 + head (collatz ((3 * m + 1) `div` 2)), m]

-- |The factorial function returns the factorial of a non-negative integer.
factorial  :: Integer -> Integer
factorial n = product [1..n]

-- |The combination function returns the number of k-length subsets of an
-- n-length set.
combination    :: Integer -> Integer -> Integer
combination n k = product [n - k + 1..n] `div` product [1..k]

-- |The maxTrianglePath function returns the maximum sum of all paths in
-- a triangular grid. The grid must be entered as a list of lists where
-- the first list contains one number, the second list contains two numbers,
-- etc. The only paths considered are those that start from the top (first list)
-- and move to an adjacent number in the next row (i.e., an element in the next
-- list with the same or 1-higher index).
maxTrianglePath      :: (Num a, Ord a) => [[a]] -> a
maxTrianglePath xss
    | length xss == 1 = head $ head xss
    | otherwise       = maxTrianglePath (init (init xss) ++ [zipWith (+) (last $ init xss) (f $ last xss)])
        where f []       = []
              f [x]      = [x]
              f (x:y:xs) = if x >= y then x:f (y:xs) else y:f (y:xs)
